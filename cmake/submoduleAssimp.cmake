
message(STATUS "Subproject: Assimp...")


# write cmake config
file( WRITE "${CMAKE_BINARY_DIR}/Assimp/CMakeLists.txt"
"
cmake_minimum_required(VERSION 3.5)

project(Assimp_install NONE)

set(RADIUM_SUBMODULES_BUILD_TYPE ${RADIUM_SUBMODULES_BUILD_TYPE})
set(RADIUM_SUBMODULES_INSTALL_DIRECTORY \"${RADIUM_SUBMODULES_INSTALL_DIRECTORY}\")
set(CMAKE_C_COMPILER \"${CMAKE_C_COMPILER}\")
set(CMAKE_CXX_COMPILER \"${CMAKE_CXX_COMPILER}\")
set(CMAKE_CXX_STANDARD ${CMAKE_CXX_STANDARD})
set(CMAKE_CXX_FLAGS \"${CMAKE_CXX_FLAGS}\")
set(CMAKE_CXX_FLAGS_DEBUG \"${CMAKE_CXX_FLAGS_DEBUG}\")
set(CMAKE_CXX_FLAGS_RELEASE \"${CMAKE_CXX_FLAGS_RELEASE}\")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO \"${CMAKE_CXX_FLAGS_RELWITHDEBINFO}\")
set(CMAKE_SHARED_LINKER_FLAGS \"${CMAKE_SHARED_LINKER_FLAGS}\")
set(CMAKE_PREFIX_PATH \"${CMAKE_PREFIX_PATH}\")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY \"${CMAKE_RUNTIME_OUTPUT_DIRECTORY}\")

if(\${RADIUM_SUBMODULES_BUILD_TYPE} MATCHES Debug)
    set(ASSIMPLIBNAME assimpd)
else()
    set(ASSIMPLIBNAME assimp)
endif()

set(ASSIMP_INCLUDE_DIR \${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/include)
if(APPLE)
    set(ASSIMP_LIBRARIES \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/lib/lib\${ASSIMPLIBNAME}.dylib\")
elseif(UNIX)
    set(ASSIMP_LIBRARIES \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/lib/lib\${ASSIMPLIBNAME}.so\")
elseif(MINGW)
    set(ASSIMP_DLL \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/bin/lib\${ASSIMPLIBNAME}.dll\")
    set(ASSIMP_LIBRARIES \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/lib/lib\${ASSIMPLIBNAME}.dll.a\")
elseif(MSVC)
    # in order to prevent DLL hell, each of the DLLs have to be suffixed with the major version and msvc prefix
    if(MSVC70 OR MSVC71)
        set(MSVC_PREFIX \"vc70\")
    elseif(MSVC80)
        set(MSVC_PREFIX \"vc80\")
    elseif(MSVC90)
        set(MSVC_PREFIX \"vc90\")
    elseif(MSVC10)
        set(MSVC_PREFIX \"vc100\")
    elseif(MSVC11)
        set(MSVC_PREFIX \"vc110\")
    elseif(MSVC12)
        set(MSVC_PREFIX \"vc120\")
    else()
        set(MSVC_PREFIX \"vc140\")
    endif()

    # Very ugly fallback, forcing to rename libs and dll generated by assimp with the name used on other OS.
    # The idea is to avoid taking care of this in the FindRadium.cmake
    if(RADIUM_SUBMODULES_BUILD_TYPE MATCHES Debug)
        set(ASSIMP_LIBRARIES_COMPATNAME \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/lib/assimp-\${MSVC_PREFIX}-mtd.lib\")
        set(ASSIMP_DLL_COMPATNAME \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/bin/assimp-\${MSVC_PREFIX}-mtd.dll\")
    else()
        set(ASSIMP_LIBRARIES_COMPATNAME \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/lib/assimp-\${MSVC_PREFIX}-mt.lib\")
        set(ASSIMP_DLL_COMPATNAME \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/bin/assimp-\${MSVC_PREFIX}-mt.dll\")
    endif()

    set(ASSIMP_LIBRARIES \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/lib/\${ASSIMPLIBNAME}.lib\")
    set(ASSIMP_DLL \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/bin/\${ASSIMPLIBNAME}.dll\")
endif()

include(ExternalProject)
ExternalProject_Add(
    assimp
    # where the source will live
    SOURCE_DIR \"${CMAKE_SOURCE_DIR}/3rdPartyLibraries/Assimp\"

    # override default behaviours
    UPDATE_COMMAND \"\"
    GIT_SUBMODULES 3rdPartyLibraries/Assimp

    # set the installation to installed/assimp
    INSTALL_DIR \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}\"
    BUILD_BYPRODUCTS \"\${ASSIMP_LIBRARIES}\"
    CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
        -DASSIMP_BUILD_ASSIMP_TOOLS=False
        -DASSIMP_BUILD_SAMPLES=False
        -DASSIMP_BUILD_TESTS=False
        -DASSIMP_INSTALL_PDB=False
        -DCMAKE_C_COMPILER=\${CMAKE_C_COMPILER}
        -DCMAKE_CXX_COMPILER=\${CMAKE_CXX_COMPILER}
        -DCMAKE_CXX_STANDARD=\${CMAKE_CXX_STANDARD}
        -DCMAKE_CXX_FLAGS=\${CMAKE_CXX_FLAGS}
        -DCMAKE_CXX_FLAGS_DEBUG=\${CMAKE_CXX_FLAGS_DEBUG}
        -DCMAKE_CXX_FLAGS_RELEASE=\${CMAKE_CXX_FLAGS_RELEASE}
        -DCMAKE_CXX_FLAGS_RELWITHDEBINFO=\${CMAKE_CXX_FLAGS_RELWITHDEBINFO}
        -DCMAKE_SHARED_LINKER_FLAGS=\${CMAKE_SHARED_LINKER_FLAGS}
        -DCMAKE_BUILD_TYPE=\${RADIUM_SUBMODULES_BUILD_TYPE}
        -DCMAKE_PREFIX_PATH=\${CMAKE_PREFIX_PATH}
        -DINSTALL_NAME_DIR=\${CMAKE_RUNTIME_OUTPUT_DIRECTORY} #override rpath to solve run bug on MACOSX
)
"
)

# run cmake, build and install
execute_process(COMMAND ${CMAKE_COMMAND} -G ${CMAKE_GENERATOR} .
    RESULT_VARIABLE result
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}/Assimp"
)
if(result)
    message(FATAL_ERROR "CMake step for Assimp failed: ${result}")
endif()

execute_process(COMMAND ${CMAKE_COMMAND} --build . -- -j8
    RESULT_VARIABLE result
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}/Assimp"
)
if(result)
    message(FATAL_ERROR "Build step for Assimp failed: ${result}")
endif()

# path to assimp-config.cmake should be in CMAKE_PREFIX_PATH
set(CMAKE_PREFIX_PATH "${CMAKE_PREFIX_PATH}" "${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/lib/cmake/assimp-4.1/")

message(STATUS "Subproject: Assimp...Done")

