
message(STATUS "Subproject: Eigen...")


# write cmake config
file( WRITE "${CMAKE_BINARY_DIR}/Eigen/CMakeLists.txt"
"
cmake_minimum_required(VERSION 3.5)

project(Eigen_install NONE)

set(RADIUM_SUBMODULES_BUILD_TYPE ${RADIUM_SUBMODULES_BUILD_TYPE})
set(RADIUM_SUBMODULES_INSTALL_DIRECTORY \"${RADIUM_SUBMODULES_INSTALL_DIRECTORY}\")
set(CMAKE_C_COMPILER \"${CMAKE_C_COMPILER}\")
set(CMAKE_CXX_COMPILER \"${CMAKE_CXX_COMPILER}\")
set(CMAKE_CXX_STANDARD ${CMAKE_CXX_STANDARD})
set(CMAKE_CXX_FLAGS \"${CMAKE_CXX_FLAGS}\")
set(CMAKE_CXX_FLAGS_DEBUG \"${CMAKE_CXX_FLAGS_DEBUG}\")
set(CMAKE_CXX_FLAGS_RELEASE \"${CMAKE_CXX_FLAGS_RELEASE}\")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO \"${CMAKE_CXX_FLAGS_RELWITHDEBINFO}\")
set(CMAKE_SHARED_LINKER_FLAGS \"${CMAKE_SHARED_LINKER_FLAGS}\")
set(CMAKE_PREFIX_PATH \"${CMAKE_PREFIX_PATH}\")

include(ExternalProject)
ExternalProject_Add(
    eigen

    # where the source will live
    SOURCE_DIR \"${CMAKE_SOURCE_DIR}/3rdPartyLibraries/Eigen\"

    # override default behaviours
    UPDATE_COMMAND \"\"
    GIT_SUBMODULES 3rdPartyLibraries/Eigen

    # set the installatin to root
    INSTALL_DIR \"\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}\"
    CMAKE_ARGS
        -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
        -DINCLUDE_INSTALL_DIR=\${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/include
        -DEIGEN_TEST_CXX11=ON
        -DBUILD_TESTING=OFF
        -DCMAKE_BUILD_TYPE=\${RADIUM_SUBMODULES_BUILD_TYPE}
        -DCMAKE_C_COMPILER=\${CMAKE_C_COMPILER}
        -DCMAKE_CXX_COMPILER=\${CMAKE_CXX_COMPILER}
        -DCMAKE_CXX_STANDARD=\${CMAKE_CXX_STANDARD}
        -DCMAKE_CXX_FLAGS=\${CMAKE_CXX_FLAGS}
        -DCMAKE_CXX_FLAGS_DEBUG=\${CMAKE_CXX_FLAGS_DEBUG}
        -DCMAKE_CXX_FLAGS_RELEASE=\${CMAKE_CXX_FLAGS_RELEASE}
        -DCMAKE_CXX_FLAGS_RELWITHDEBINFO=\${CMAKE_CXX_FLAGS_RELWITHDEBINFO}
        -DCMAKE_SHARED_LINKER_FLAGS=\${CMAKE_SHARED_LINKER_FLAGS}
        -DCMAKE_PREFIX_PATH=\${CMAKE_PREFIX_PATH}
    STEP_TARGETS install
)
"
)

# run cmake, build and install
execute_process(COMMAND ${CMAKE_COMMAND} -G ${CMAKE_GENERATOR} .
    RESULT_VARIABLE result
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}/Eigen"
)
if(result)
    message(FATAL_ERROR "CMake step for Eigen failed: ${result}")
endif()

execute_process(COMMAND ${CMAKE_COMMAND} --build . -- -j8
    RESULT_VARIABLE result
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}/Eigen"
)
if(result)
    message(FATAL_ERROR "Build step for Eigen failed: ${result}")
endif()

# path to Eigen3Config.cmake should be in CMAKE_PREFIX_PATH
set(CMAKE_PREFIX_PATH "${CMAKE_PREFIX_PATH}" "${RADIUM_SUBMODULES_INSTALL_DIRECTORY}/share/eigen3/cmake/")

message(STATUS "Subproject: Eigen...Done")
